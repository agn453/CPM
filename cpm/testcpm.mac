	cseg
;
; A simple CP/M program for testing
;
	.z80

start:	ld	c,9	; print string
	ld	de,msg0
	call	5
	ld	c,1	; read a character
	call	5
        push	af	; save it
	ld	e,a
	ld	c,2	; echo it back
	call	5
	ld	c,9
	ld	de,msg1
	call	5
	pop	af
	call	ahex	; print char in hex
	ld	c,9
	ld	de,msg2
	call	5

	call	delay	; Give them a chance to type something

getc:	ld	e,0FFh	; direct console I/O test
	ld	c,6
	call	5

	or	a	; anything typed?
	jr	nz,gotc	; yes

	ld	c,9
	ld	de,msg4
	call	5
	jr	done

gotc:	push	af
	ld	c,9
	ld	de,msg3
	call	5
	pop	af
	call	ahex

done:	ld	c,9	; new line and exit
	ld	de,msg5
	jp	5

; Print value in A to console in hexadecimal

ahex:	push	af
	rrca
	rrca
	rrca
	rrca
	call	chex
	pop	af
chex:	and	0Fh
	add	a,90h
	daa
	adc	a,40h
	daa
	ld	e,a
	ld	c,2		; Console output
	jp	5

delay:	push	bc
	push	hl
	ld	b,64
dlyn:	call	dly0
	djnz	dlyn
	pop	hl
	pop	bc
	ret

dly0:	ld	hl,0
dly1:	dec	hl
	ld	a,h
	or	l
	jr	nz,dly1
	ret

msg0:	defb	13,10,'Hello from CP/M'
	defb	13,10,10,'Press a key : $'

msg1:	defb	13,10,10,'You typed a character with this value (in hex) $'

msg2:	defb	13,10,10,'Now checking direct console I/O'
	defb	13,10,10,"I'm going to delay a bit. You may type some keys"
	defb	13,10,'or not. I will let you know if I see anything...'
	defb	13,10,'$'

msg3:	defb	13,10,'This time you typed $'

msg4:	defb	13,10,'You typed nothing$'

msg5:	defb	13,10,'$'

	end	start

